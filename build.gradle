plugins {
    id "java-library"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group "com.sxtanna.mc"
version "1.0.0"

repositories {
    mavenCentral()
    
    maven {
        url = uri("https://repo.aikar.co/content/groups/aikar/")
    }
    maven {
        url = uri("https://papermc.io/repo/repository/maven-public/")
    }
}

dependencies {
    api "ch.jalu:configme:1.2.0"
    api "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    
    compileOnly "org.jetbrains:annotations:20.1.0"
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-parameters"]
    
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


sourceSets {
    test.compileClasspath += configurations.compileOnly
    test.runtimeClasspath += configurations.compileOnly
}

shadowJar {
    archiveClassifier.set("")
    
    relocate "ch.jalu", "com.sxtanna.mc.acolytes.libs"
    relocate "co.aikar.commands", "com.sxtanna.mc.acolytes.libs.acf"
    relocate "co.aikar.locales", "com.sxtanna.mc.acolytes.libs.locales"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: project.version]
    }
}


task copyToTestServerDir(type: Copy) {
    from shadowJar
    into "C:\\Users\\Sxtanna\\Desktop\\Minecraft\\VibingCloud\\plugins"
}